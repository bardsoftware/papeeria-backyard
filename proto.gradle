
buildscript {
    ext {
        grpc_version = "1.24.1"
        kotlin_version = "1.3.71"
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.+'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'com.google.protobuf'
apply plugin: 'maven-publish'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

sourceSets {
    main {
        proto {
        }
        java {
            srcDirs "$projectDir/src/generated/main/java"
        }
        kotlin {
            srcDirs "$projectDir/src/main/kotlin"
            include "com/bardsoftware/papeeria/backend/SSL.kt"
        }
    }
}

dependencies {
    api "com.google.cloud:google-cloud-core:1.93.3"
    api "com.google.cloud:google-cloud-secretmanager:0.3.0"

    implementation 'com.google.protobuf:protobuf-java:3.11.4'
    implementation "io.grpc:grpc-protobuf:$grpc_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.slf4j:slf4j-api:1.7.30'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.+'
        generatedFilesBaseDir = "$projectDir/src/generated"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.+'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

group 'com.bardsoftware.papeeria.backend'
version "2020.03.20"

publishing {
    repositories {
        maven {
            name "papeeria-maven"
            url "gcs://papeeria-maven-repo/maven2"
        }
    }
    publications {
        libbackyardApi(MavenPublication) {
            artifactId = 'libbackyard-proto'
            from components.java
        }
    }
}


